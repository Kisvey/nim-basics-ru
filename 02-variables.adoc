= Присваивание имён значениям



Может быть полезно давать значениям в наших программах имена, чтобы помочь нам отслеживать их.
Если мы спрашиваем у пользователя его/её имя, мы хотим сохранить его для дальнейшего использования, не спрашивая его снова и снова каждый раз, когда нам нужно сделать с ним какие-то вычисления.

В примере `pi = 3.14`, имя `pi` связано со значением `3.14`.
Из нашего опыта мы можем сказать, что тип переменной `pi` — это (десятичное) число.

Другой пример — `firstName = Alice`, где `firstName` — это имя переменной со значением `Alice`.
Мы бы сказали, что тип этой переменной — это слово.

В языках программирования это работает аналогично.
Данные присваивания имён имеют своё _имя_, _значение_ и _тип_.




== Декларация переменной

Nim это https://ru.wikipedia.org/wiki/%D0%A1%D0%B8%D1%81%D1%82%D0%B5%D0%BC%D0%B0_%D1%82%D0%B8%D0%BF%D0%BE%D0%B2[статически типизированный] язык программирования, что означает, что тип присваивания должен быть объявлен до использования значения.

В Nim мы также различаем значения, которые могут изменяться (или мутировать), и те, которые не могут, но об этом позже.
Мы можем объявить переменную (изменяемое присваивание) с помощью ключевого слова `var`, просто указав её имя и тип (значение можно добавить позже), используя следующий синтаксис:
[source]
----
var <name>: <type>
----

Если мы уже знаем значение, мы можем объявить переменную и сразу присвоить ей значение:
[source]
----
var <name>: <type> = <value>
----
NOTE: Угловые скобки(`<>`) используются для обозначения того, что вы можете изменить. +
Таким образом, `<name>` — это не буквально слово `name` в угловых скобках, а любое имя.

Nim также обладает способностью https://ru.wikipedia.org/wiki/%D0%92%D1%8B%D0%B2%D0%BE%D0%B4_%D1%82%D0%B8%D0%BF%D0%BE%D0%B2[вывода типов]: компилятор может автоматически определять тип присваивания имени по его значению, без явного указания типа.
Мы посмотрим различные типы подробнее в <<Основные типы данных, следующей главе>>.

Так что мы можем присвоить переменную без явного указания типа следующим образом:
[source]
----
var <name> = <value>
----

Вот пример того, как это выглядит в Nim.
[source]
----
var a: int  # <1>
var b = 7   # <2>
----
<1> Переменная `a` имеет тип `int` (integer) без явно определенного значения.
<2> Переменная `b` имеет значение `7`. Её тип автоматически определяется как integer.

Когда вы присваиваете имена, важно выбирать имена, которые что-то значат для вашей программы.
Простое именование их `a`, `b`, `c` и так далее быстро станет запутанным.
Невозможно использовать пробелы в имени, так как это разделит его на два.
Поэтому, если выбранное вами имя состоит из более чем одного слова, обычно его пишут в стиле `camelCase` (обратите внимание, что первая буква в имени должна быть строчной).

Заметьте, однако, что Nim не различает регистр букв и подчёркивания, что означает, что `helloWorld` и `hello_world` будут одним и тем же именем.
Исключением из этого правила является первый символ, который _различает_ регистр букв.
Имена также могут включать как цифры, так и другие символы UTF-8, даже эмодзи, если вы того пожелаете, но имейте в виду, что вам и, возможно, другим придётся их вводить.

{nbsp}

Вместо того чтобы набирать `var` для каждой переменной, несколько переменных (не обязательно одного типа) могут быть объявлены в одном блоке `var`.
В Nim блоки — это части кода с одинаковым отступом (одинаковым количеством пробелов перед первым символом), и уровень отступа по умолчанию — два пробела.
Вы увидите такие блоки повсюду в программе на Nim, не только для присваивания имён.

[source]
----
var
  c = -11
  d = "Hello"
  e = '!'
----

NOTE: В Nim Табы не допускаются в качестве отступов. +
Вы можете настроить ваш редактор кода так, чтобы при нажатии `Tab` вставлялось любое количество пробелов. +
В VS Code настройка по умолчанию — преобразование `Tab` в четыре пробела.
Это легко изменить в настройках (`Ctrl+,`), установив `"editor.tabSize": 2`.

Как и было упомянуто ранее, переменные изменяемы, то есть их значение может изменяться (несколько раз), но их тип должен оставаться таким же, как и было декларировано.

[source]
----
var f = 7           <1>

f = -3              <2>
f = 19
f = "Hello" # error <3><4>
----
<1> Переменная `f` имеет начальное значение `7` и её тип автоматически определяется как `int`.
<2> Значение переменной `f` сначала изменяется на `-3`, а затем на `19`. Оба этих значения являются целыми числами, как и исходное значение.
<3> Попытка изменить значение переменной `f` на `"Hello"` вызывает ошибку, потому что `"Hello"` не является числом, и это изменило бы тип переменной `f` с целого числа на строку.
<4> `# error` — это комментарий. Комментарии в коде Nim пишутся после символа `#`. Все, что находится после него на той же строке, будет проигнорировано.


== Неизменяемые присваивания

В отличие от переменных, объявленных с помощью ключевого слова `var`, в Nim существуют ещё два типа присваиваний, значение которых не может изменяться, один объявляется с помощью ключевого слова `const`, а другой — с помощью ключевого слова `let`.


=== Const

Значение неизменяемого присваивания, объявленного с помощью ключевого слова `const`, должно быть известно на этапе компиляции (до запуска программы).

Для примера, мы можем объявить ускорение свободного падения как `const g = 9.81` или число пи как `const pi = 3.14`, так как мы знаем их значения заранее, и эти значения не изменятся во время выполнения нашей программы.

[source]
----
const g = 35
g = -27         # error <1>

var h = -5
const i = h + 7 # error <2>
----
<1> Значение константы не может быть изменено.
<2> Переменная `h` не вычисляется на этапе компиляции (это переменная, и её значение может изменяться во время выполнения программы), следовательно, значение константы `i` не может быть известно на этапе компиляции, и это вызовет ошибку.

В некоторых языках программирования принято писать имена констант в `ALL_CAPS`.
Константы в Nim пишутся так же, как и любые другие переменные.



=== Let

Неизменяемые присваивания, объявленные с помощью ключевого слова `let`, не нуждаются в знании на этапе компиляции, их значение может быть установлено в любое время во время выполнения программы, но как только оно установлено, его значение не может измениться.

[source]
----
let j = 35
j = -27 # error <1>

var k = -5
let l = k + 7   <2>
----
<1> Значение неизменяемого присваивания не может быть изменено.
<2> В отличие от примера с `const` выше, это работает.

На практике вы будете чаще видеть/использовать `let`, чем `const`.

Пока вы можете использовать `var` для всего, вашим выбором по умолчанию должно быть `let`.
Используйте `var` только для переменных, которые будут изменяться.
